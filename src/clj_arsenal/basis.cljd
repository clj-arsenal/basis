(ns clj-arsenal.basis
  (:require
   [cljd.core :refer [IFn IExceptionInfo ExceptionInfo]]
   ["dart:async" :as async]
   ["dart:core" :as dart]))

(defn try-fn "
Dialect independent version of try with catch-anything.
" [f & {catch-fn :catch finally-fn :finally}]
  (try
    (f)
    (catch Error ex
      (when (ifn? catch-fn)
        (catch-fn ex)))
    (catch dynamic ex st
      ;; ExceptionInfo doesn't implement Exception
      ;; for some reason. And neither ExceptionInfo
      ;; nor Exception gets a StackTrace attached by
      ;; default, so we wrap either in an ExceptionInfo
      ;; that has the StackTrace attached under the `:st`
      ;; key.  The default logger for clj-arsenal.log
      ;; is aware of this key.
      (when (ifn? catch-fn)
        (catch-fn
          (if (satisfies? IExceptionInfo ex)
            (ex-info
              (if (instance? ExceptionInfo ex)
                (.-message ^ExceptionInfo ex)
                (str ex))
              (assoc (ex-data ex) :st st) ex)
            (ex-info (str ex) {:st st} ex)))))
    (finally
      (when (ifn? finally-fn)
        (finally-fn)))))

(defn error? "
Dialect-independent error check predicate.  Checks for
exceptions, errors, throwables, ex-info, etc.  Whatever
makes sense to be considered as, and handled as, an
error for the host language.
" [x]
  (or (instance? Exception x) (instance? Error x) (satisfies? IExceptionInfo x)))

(deftype ^:private Signal [!listeners]
  IFn
  (-invoke
    [this]
    (doseq [listener (vals @!listeners)]
      (listener)))
  (-apply
    [this args]
    (this)))

(defn signal "
Creates a signal, which is a callable that, when called,
invokes all listener callbacks attached to it.  Listeners
can be registered with `sig-listen` and `sig-unlisten`.
" []
  (->Signal (atom {})))

(defn signal? "
Returns true if `x` is a signal.
" [x]
  (instance? Signal x))

(defn sig-listen "
Attach a listener to `sig`, a signal. `k` is a key
that can be used to remove the listener via `sig-unlisten`;
if omitted then `f` doubles as the key.

`f` is the callback to be invoked when the signal is called.
" ([^Signal sig f]
   (sig-listen sig f f))
  ([^Signal sig k f]
   (swap! (.-!listeners sig) assoc k f)))

(defn sig-unlisten "
Removes a signal listener with the key `k`.
" [^Signal sig k]
  (swap! (.-!listeners sig) dissoc k))

(defn schedule-once "
Dialect-independent task scheduler.  Schedules `f` to be invoked
with `args`, after `delay` millseconds.

Returns a handle that can be passed to `cancel-scheduled` to cancel
the task.
" ^async/Timer [delay f & args]
  (async/Timer (dart/Duration .milliseconds delay) (fn [] (apply f args))))

(defn schedule-every "
Dialect-independent function to schedule periodic task.  Schedules
`f` to be invoked with `args` every `delay` milliseconds.

Returns a handle that can be passed to `cancel-scheduled` to
cancel it.
" ^async/Timer [delay f & args]
  (async/Timer.periodic (dart/Duration .milliseconds delay) (fn [^async/Timer _] (apply f args))))

(defn cancel-scheduled "
Cancel a scheduled task.
" [^async/Timer t]
  (.cancel t))

(defn ticker "
Creates a signal and schedules it to be called every `delay`
milliseconds.  Returns a tuple `[sig scheduled]`, where `sig`
is the signal, and `scheduled` is a handle that can be passed
to `cancel-scheduled` to cancel the periodic calls.
" [delay]
  (let [sig (signal)
        t (schedule-every delay sig)]
    [sig #(cancel-scheduled t)]))
